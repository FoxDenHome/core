{ nixpkgs, foxDenLib, ... }:
let
  util = foxDenLib.util;
  eSA = nixpkgs.lib.strings.escapeShellArg;
  mkHostSuffix = name: util.mkHash8 name;

  ifcfgRouteType = with nixpkgs.lib.types; submodule {
    options = {
      Destination = nixpkgs.lib.mkOption {
        type = str;
      };
      Gateway = nixpkgs.lib.mkOption {
        type = str;
      };
    };
  };

  hostType = with nixpkgs.lib.types; submodule {
    options = {
      dns = {
        name = nixpkgs.lib.mkOption {
          type = str;
          default = "";
        };
        zone = nixpkgs.lib.mkOption {
          type = str;
          default = "foxden.network";
        };
        ttl = nixpkgs.lib.mkOption {
          type = int;
          default = 3600;
        };
      };
      vlan = nixpkgs.lib.mkOption {
        type = int;
      };
      addresses = nixpkgs.lib.mkOption {
        type = listOf str;
      };
    };
  };

  routeType = with nixpkgs.lib.types; submodule {
    options = {
      Destination = nixpkgs.lib.mkOption {
        type = str;
        default = "default";
      };
      Gateway = nixpkgs.lib.mkOption {
        type = str;
        default = "";
      };
    };
  };

  mkHostInfo = (name: {
    namespace = "/run/netns/host-${name}";
    unit = "netns-host-${name}.service";
    resolvConf = "/etc/foxden/hosts/resolv.conf";
    suffix = mkHostSuffix name;
  });

  getByName = (config: name: {
    inherit name;
    info = mkHostInfo name;
    config = config.foxDen.hosts.hosts.${name};
  });
in
{
  getByName = getByName;

  nixosModule = ({ config, pkgs, foxDenLib, ... }:
  let
    hosts = config.foxDen.hosts.hosts;
    ifcfg = config.foxDen.hosts.ifcfg;

    hostDriver = foxDenLib.hosts.drivers.${config.foxDen.hosts.driver};

    hostDriverConfig = hostDriver.build
      { inherit ifcfg hosts pkgs; driverOpts = config.foxDen.hosts.driverOpts; };

    netnsRoutes = (hostDriverConfig.routes or ifcfg.routes) ++ config.foxDen.hosts.routes;
  in
  {
    options.foxDen.hosts = {
      hosts = with nixpkgs.lib.types; nixpkgs.lib.mkOption {
        type = attrsOf hostType;
        default = {};
      };

      ifcfg = with nixpkgs.lib.types; {
        dns = nixpkgs.lib.mkOption {
          type = listOf str;
        };
        addresses = nixpkgs.lib.mkOption {
          type = listOf str;
          default = [];
        };
        routes = nixpkgs.lib.mkOption {
          type = listOf ifcfgRouteType;
          default = [];
        };
        interface = with nixpkgs.lib.types; nixpkgs.lib.mkOption {
          type = str;
        };
        network = with nixpkgs.lib.types; nixpkgs.lib.mkOption {
          type = str;
        };
      };

      routes = with nixpkgs.lib.types; nixpkgs.lib.mkOption {
        type = listOf routeType;
        default = [];
      };

      driver = with nixpkgs.lib.types; nixpkgs.lib.mkOption {
        type = enum (nixpkgs.lib.attrsets.attrNames foxDenLib.hosts.drivers);
      };

      driverOpts = nixpkgs.lib.mkOption {
        type = hostDriver.driverOptsType;
        default = {};
      };
    };

    config = {
      foxDen.hosts.ifcfg.network = nixpkgs.lib.mkDefault "40-${ifcfg.interface}";

      foxDen.dns.records = nixpkgs.lib.flatten (map
          (host: let
            mkRecord = (addr: nixpkgs.lib.mkIf (host.dns.name != "") {
              zone = host.dns.zone;
              name = host.dns.name;
              type = if (util.isIPv6 addr) then "AAAA" else "A";
              ttl = host.dns.ttl;
              value = util.removeIpCidr addr;
              horizon = if (util.isPrivateIP addr) then "internal" else "external";
            });
          in
          (map mkRecord host.addresses))
        (nixpkgs.lib.attrsets.attrValues hosts));

      environment.etc."foxden/hosts/resolv.conf".text = ''
        # Generated by foxDen
        ${nixpkgs.lib.concatMapStrings (ns: "nameserver ${ns}\n") config.foxDen.hosts.ifcfg.dns}
      '';

      systemd = nixpkgs.lib.mkMerge [
        hostDriverConfig.config.systemd
        {
          # Configure host/primary network/bridge
          network.networks."${config.foxDen.hosts.ifcfg.network}" = {
            name = ifcfg.interface;
            routes = ifcfg.routes;
            address = ifcfg.addresses;
            dns = ifcfg.dns;

            networkConfig = {
              DHCP = "no";
              IPv6AcceptRA = false;
            };
          };

          # Configure each host's NetNS
          services = (nixpkgs.lib.attrsets.listToAttrs (map ({ name, value }: let
            host = getByName config name;
            namespace = (nixpkgs.lib.strings.removePrefix "/run/netns/" host.info.namespace);

            ipCmd = eSA "${pkgs.iproute2}/bin/ip";
            ipInNsCmd = "${ipCmd} netns exec ${eSA namespace} ${ipCmd}";

            mkServiceInterface = hostDriverConfig.serviceInterface or (name: host: "host-${mkHostSuffix name}");
            serviceInterface = mkServiceInterface name value;
            driverRunParams = { inherit ipCmd ipInNsCmd serviceInterface host; };
          in
          {
            name = (nixpkgs.lib.strings.removeSuffix ".service" host.info.unit);
            value = {
              description = "NetNS ${namespace}";
              unitConfig = {
                StopWhenUnneeded = true;
              };
              serviceConfig = {
                Type = "oneshot";
                RemainAfterExit = true;

                ExecStart = [
                  "-${ipCmd} netns del ${eSA namespace}"
                  "${ipCmd} netns add ${eSA namespace}"
                  "${ipInNsCmd} addr add 127.0.0.1/8 dev lo"
                  "${ipInNsCmd} addr add ::1/128 dev lo noprefixroute"
                  "${ipInNsCmd} link set lo up"
                ]
                ++ (hostDriverConfig.execStart driverRunParams)
                ++ [ "${ipCmd} link set ${eSA serviceInterface} netns ${eSA namespace}" ]
                ++ (map (addr:
                      "${ipInNsCmd} addr add ${eSA addr} dev ${eSA serviceInterface}")
                      value.addresses)
                ++ [ "${ipInNsCmd} link set ${eSA serviceInterface} up" ]
                ++ (map (route:
                      "${ipInNsCmd} route add ${eSA route.Destination} dev ${eSA serviceInterface}" + (if (route.Gateway or "") != "" then " via ${eSA route.Gateway}" else ""))
                      netnsRoutes);

                ExecStop =
                  (hostDriverConfig.execStop driverRunParams)
                  ++ [
                    "${ipCmd} netns del ${eSA namespace}"
                  ];
              };
            };
          }) (nixpkgs.lib.attrsets.attrsToList hosts)));
        }
      ];
    };
  });
}
